subprojects {

    apply plugin: 'java-library'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    group = 'jseproject'
    version = '1.1.0'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }

}

project(':library') {

    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    tasks.withType(Javadoc).configureEach { enabled = false }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                url projectDir.absolutePath + '/build/repository'
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = 'io.github.jseproject'
                artifactId = 'jse-api'
                version = version
                from components.java
                pom {
                    name = 'JavaSound Enhancement API'
                    description = 'Extended API for the JavaSound subsystem of the Java Platform.'
                    url = 'https://github.com/jseproject/jse-api'
                    licenses {
                        license {
                            name = 'BSD 3-Clause \"New\" or \"Revised\" License'
                            url = 'http://opensource.org/licenses/BSD-3-Clause'
                        }
                    }
                    developers {
                        developer {
                            id = 'Tianscar'
                            name = 'Naoko Mitsurugi'
                            email = 'tianscar@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com/jseproject/jse-api.git'
                        developerConnection = 'scm:git:git@github.com/jseproject/jse-api.git'
                        url = 'https://github.com/jseproject/jse-api'
                    }
                }
            }
        }
    }

    signing {
        sign(publishing.publications.mavenJava)
    }

}
